; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "EncounterPRO-OS New Database"

; Source Root
!define SOURCE_ROOT "\\ICT1\ICTFileStore1\Open Source\Builds"

; Component Versions
!define NewDB_Version   "200.3"

; Other definitions
!define PRODUCT_VERSION "${NewDB_Version}"
!define PRODUCT_PUBLISHER "EncounterPRO.ORG"
!define PRODUCT_WEB_SITE "http://www.encounterpro.org"
!define Required_Dotnet_VERSION   'v4.0'

; Module Locations
!define NewDB_SOURCE  '${SOURCE_ROOT}\EncounterPRO-OS\New Database\${NewDB_Version}\Files'

; Installation constants
!define COMMONFILES_TARGET "EncounterPRO-OS"

; Include install functions
!include "..\plugins\eproinstallfunctions.nsh"

; FileFunc
!include "FileFunc.nsh"
!insertmacro GetFileName
!insertmacro GetFileVersion  

; InstallLib support
!include LogicLib.nsh
!include Library.nsh

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"

    ; Welcome page
;    !insertmacro MUI_PAGE_WELCOME
    ; License page
;    !insertmacro MUI_PAGE_LICENSE "\\techserv\builds\Common Files\Eula.txt"
    
    ; Instfiles page
    !insertmacro MUI_PAGE_INSTFILES
    ; Finish page
;    !insertmacro MUI_PAGE_FINISH

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve Files

;variables
var DBServer
var NewDBName

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "EncounterPRO-OS New Database ${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\EncounterPRO.ORG\EncounterPRO-OS"
ShowInstDetails show
AutoCloseWindow false

Function PreInstall
  Call CheckBitness
  ;MessageBox MB_OK "$Bitness bit OS"

  Call getWindowsVersion
  ${If} $WinVer == 'not NT'
      MessageBox MB_OK "This program Windows XP, Windows 2003 or Later.  Setup cannot continue."
      Abort "This program Windows XP, Windows 2003 or Later."
  ${EndIf}
  ;MessageBox MB_OK "$WinVer"
  
  Call CheckIsAdminUser
FunctionEnd

Function FoundSQLCmd
    StrCpy $R0 $R9
    StrCpy $0 StopLocate
    Push $0
FunctionEnd

Section -Main SEC0000
    SearchPath $R0 SQLCmd.exe
    StrCmp $R0 "" +2 0
    goto SQLCmdFound

    ${Locate} "C:\Program Files\Microsoft SQL Server\100\Tools\Binn" "/L=F /M=SQLCMD.EXE" "FoundSQLCmd"
    IfErrors 0 SQLCmdFound
    
    ${Locate} "C:\" "/L=F SQLCmd.exe" "FoundSQLCmd"
    IfErrors 0 SQLCmdFound

    MessageBox MB_OK "Unable to locate the SQLCmd.exe utility program.  Please check your SQL Server installation and try again."
    Abort "Unable to locate the SQLCmd.exe utility program"


  SQLCmdFound:
    ;MessageBox MB_OK "SQLCMD=$R0"
  
  Push "DBServer"         ; push the search string onto the stack
  Push ""   ; push a default value onto the stack
  Call GetParameterValue
  Pop $DBServer
  
  Push "NewDBName"         ; push the search string onto the stack
  Push ""   ; push a default value onto the stack
  Call GetParameterValue
  Pop $NewDBName
 

  ${If} $DBServer == ''
      MessageBox MB_OK "The DBServer param was not supplied"
      Abort "Please supply the DBServer param"
  ${EndIf}
  ${If} $NewDBName == ''
      MessageBox MB_OK "The NewDBName param was not supplied"
      Abort "Please supply the NewDBName param"
  ${EndIf}

    SetOutPath $INSTDIR\NewDatabase
    SetDetailsPrint both
    DetailPrint "Creating a new EncounterPRO-OS Database..."
    
    DetailPrint "Extracting Database Files..."
    SetDetailsPrint textonly
    File "${NewDB_SOURCE}\EncounterPRO-OS NewDB ${NewDB_Version}.bak"
    File "${NewDB_SOURCE}\Restore Epro Database.sql"
    SetDetailsPrint both
    
    DetailPrint "Installing New Database.  This may take several minutes..."
    FileOpen $4 "$INSTDIR\NewDatabase\RestoreDB.bat" w
    FileWrite $4 '"$R0" -X -E -S "$DBServer" -i "$INSTDIR\NewDatabase\Restore Epro Database.sql" -v NewDBName = "$NewDBName" backupfilepath = "$INSTDIR\NewDatabase\EncounterPRO-OS NewDB ${NewDB_Version}.bak"'
    FileClose $4
    ExecWait "$INSTDIR\NewDatabase\RestoreDB.bat"
    ;MessageBox MB_OK 'sqlcmd -X -E -S "$DBServer" -i "$INSTDIR\Restore Epro Database.sql" -v NewDBName = "$NewDBName" backupfilepath = "$INSTDIR\EncounterPRO-OS NewDB ${NewDB_Version}.bak"'
    ;nsExec::Exec 'sqlcmd -X -E -S "$DBServer" -i "$INSTDIR\NewDatabase\Restore Epro Database.sql" -v NewDBName = "$NewDBName" backupfilepath = "$INSTDIR\NewDatabase\EncounterPRO-OS NewDB ${NewDB_Version}.bak"'
    
    WriteINIStr "$INSTDIR\EncounterPRO.ini" "<Default>" "dbserver" $DBServer
    WriteINIStr "$INSTDIR\EncounterPRO.ini" "<Default>" "dbname" $NewDBName
    WriteINIStr "$INSTDIR\EncounterPRO.ini" "<Default>" "dbms" "SNC"
    
    ;DetailPrint "Deleting SQL Server Express Installation Files..."
    ;!insertmacro RemoveFilesAndSubDirs "$INSTDIR\NewDatabase"
SectionEnd

Section '-Post' SecPost
SectionEnd

Function .onInit
  Call PreInstall
FunctionEnd

