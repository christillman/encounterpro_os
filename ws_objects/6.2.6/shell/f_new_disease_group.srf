HA$PBExportHeader$f_new_disease_group.srf
global type f_new_disease_group from function_object
end type

forward prototypes
global function string f_new_disease_group ()
end prototypes

global function string f_new_disease_group ();str_popup popup
str_popup_return popup_return
long ll_sts
long ll_count
u_ds_data luo_data
long ll_disease_id
string ls_disease_group
string ls_null

setnull(ls_null)

DO WHILE true
	popup.title = "Enter the name of the new Disease Group"
	popup.displaycolumn = 24
	openwithparm(w_pop_prompt_string, popup)
	popup_return = message.powerobjectparm
	if popup_return.item_count <> 1 then return ls_null
	
	ls_disease_group = popup_return.items[1]
	
	SELECT count(*)
	INTO :ll_count
	FROM c_Disease_Group
	WHERE disease_group = :ls_disease_group;
	if not tf_check() then return ls_null
	if ll_count > 0 then
		openwithparm(w_pop_message, "There is already a Disease Group with that name.  Please enter a different name for the new Disease Group.")
	else
		exit
	end if
LOOP

INSERT INTO c_Disease_Group (
	disease_group,
	description,
	status)
VALUES (
	:ls_disease_group,
	:ls_disease_group,
	'OK');
if not tf_check() then return ls_null

return ls_disease_group

end function

