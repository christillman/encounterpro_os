$PBExportHeader$f_order_document.srf
global type f_order_document from function_object
end type

forward prototypes
global function long f_order_document (string ps_cpr_id, long pl_encounter_id, string ps_context_object, long pl_object_key, string ps_report_id, string ps_document_route, string ps_ordered_for, string ps_ordered_by, string ps_description, string ps_purpose, boolean pb_create_now, boolean pb_send_now, string ps_create_from, string ps_send_from, boolean pb_runtime_configured, str_attributes pstr_attributes)
end prototypes

global function long f_order_document (string ps_cpr_id, long pl_encounter_id, string ps_context_object, long pl_object_key, string ps_report_id, string ps_document_route, string ps_ordered_for, string ps_ordered_by, string ps_description, string ps_purpose, boolean pb_create_now, boolean pb_send_now, string ps_create_from, string ps_send_from, boolean pb_runtime_configured, str_attributes pstr_attributes);long ll_patient_workplan_id
long ll_patient_workplan_item_id
u_ds_data luo_data
long ll_count
u_component_wp_item_document luo_document
integer li_sts
long ll_material_id
long i

setnull(ll_patient_workplan_id)

if isnull(ps_purpose) then
	SELECT a.value
	INTO :ps_purpose
	FROM c_Report_Attribute a
	WHERE a.report_id = :ps_report_id
	AND a.attribute_sequence = (SELECT max(x.attribute_sequence)
								FROM c_Report_Attribute x
								WHERE x.report_id = :ps_report_id
								AND x.attribute = 'Purpose'
								AND x.component_attribute = 'N');
	if not tf_check() then return -1
	
	if isnull(ps_purpose) then
		log.log(po_null, "f_order_document:0025", "Document must have a purpose", 4)
		return -1
	end if
end if

if trim(ps_create_from) = "" then setnull(ps_create_from)
if trim(ps_send_from) = "" then setnull(ps_send_from)

// if the send_now is true then epro server can create and send in the same pass, so only set the initial status to "Creating" if
// the create_now flag is set and the send_now flag is not set
// 6 possibilities:
// Create now From Client, Send now from client
// Create now From Client, Send now from server
// Create now From Client, Send later
// Create now from server, send now from server
// Create now from server, send later
// Create later from client, send later
// Create later from server, send later

setnull(ll_material_id)

ll_patient_workplan_item_id = sqlca.jmj_order_document2( ps_cpr_id, &
																			pl_encounter_id, &
																			ps_context_object, &
																			pl_object_key, &
																			ps_report_id, &
																			ps_purpose, &
																			ps_document_route, &
																			ps_ordered_for, &
																			0, &
																			ps_description, &
																			ps_ordered_by, &
																			current_scribe.user_id, &
																			ll_material_id, &
																			ps_create_from, &
																			ps_send_from)
if not tf_check() then return -1
if ll_patient_workplan_item_id <= 0 then return -1

// Save any other attributes passed in
for i = 1 to pstr_attributes.attribute_count
	sqlca.sp_add_workplan_item_attribute(	ps_cpr_id, &
														ll_patient_workplan_id, &
														ll_patient_workplan_item_id, &
														pstr_attributes.attribute[i].attribute, &
														pstr_attributes.attribute[i].value, &
														current_scribe.user_id, &
														current_user.user_id)
next

// If the runtime attributes have already been collected then set the flag in the p_Patient_WP_Item record
if pb_runtime_configured then

	UPDATE p_Patient_WP_Item
	SET runtime_configured_flag = 'Y'
	WHERE patient_workplan_item_id = :ll_patient_workplan_item_id;
	if not tf_check() then return -1
end if

// If the caller wants to send the document now, then do so
if pb_send_now then
	luo_document = CREATE u_component_wp_item_document
	
	li_sts = luo_document.initialize(ll_patient_workplan_item_id)
	if li_sts < 0 then return -1
	
	li_sts = luo_document.document_send()
	if li_sts < 0 then return -1
	
	DESTROY luo_document
elseif pb_create_now then
	// We only check the create_now if the send_now is false because a true send_now will automatically do a create_now
	if gnv_app.cpr_mode = "CLIENT" then
		luo_document = CREATE u_component_wp_item_document
		
		li_sts = luo_document.initialize(ll_patient_workplan_item_id)
		if li_sts < 0 then return -1
		
		if isnull(ps_create_from) or lower(ps_create_from) = "client" then
			li_sts = luo_document.document_create()
			if li_sts < 0 then return -1
		else
			// If we're creating now from the the server then set the status to "Creating" so the server will get the message
			li_sts = luo_document.set_progress("Creating")
			if li_sts < 0 then return -1
		end if
		
		DESTROY luo_document
	else
		luo_document = CREATE u_component_wp_item_document
		
		li_sts = luo_document.initialize(ll_patient_workplan_item_id)
		if li_sts < 0 then return -1
		
		li_sts = luo_document.document_create()
		if li_sts < 0 then return -1
		
		DESTROY luo_document
	end if
end if

return ll_patient_workplan_item_id

end function

