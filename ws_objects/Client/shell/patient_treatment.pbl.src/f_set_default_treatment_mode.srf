$PBExportHeader$f_set_default_treatment_mode.srf
global type f_set_default_treatment_mode from function_object
end type

forward prototypes
global function integer f_set_default_treatment_mode (string ps_treatment_type, string ps_treatment_key, string ps_treatment_mode)
end prototypes

global function integer f_set_default_treatment_mode (string ps_treatment_type, string ps_treatment_key, string ps_treatment_mode);string ls_treatment_mode
datetime ldt_now

ldt_now = datetime(today(), now())

// If the mode is null then we're really deleting the default
if isnull(ps_treatment_mode) then
	DELETE o_Treatment_Type_Default_Mode
	WHERE treatment_type = :ps_treatment_type
	AND treatment_key = :ps_treatment_key
	AND office_id = :gnv_app.office_id;
	if not tf_check() then return -1
	return 1
end if

UPDATE o_Treatment_Type_Default_Mode
SET treatment_mode = :ps_treatment_mode
WHERE treatment_type = :ps_treatment_type
AND treatment_key = :ps_treatment_key
AND office_id = :gnv_app.office_id;
if not tf_check() then return -1
if sqlca.sqlnrows = 0 then
	// If we didn't find the record then insert one
	INSERT INTO o_Treatment_Type_Default_Mode (
		treatment_type,
		treatment_key,
		office_id,
		treatment_mode,
		created,
		created_by)
	VALUES (
		:ps_treatment_type,
		:ps_treatment_key,
		:gnv_app.office_id,
		:ps_treatment_mode,
		:ldt_now,
		:current_scribe.user_id );
	if not tf_check() then return -1
end if

return 1

end function

