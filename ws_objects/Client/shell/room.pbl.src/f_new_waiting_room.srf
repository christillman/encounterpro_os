$PBExportHeader$f_new_waiting_room.srf
global type f_new_waiting_room from function_object
end type

forward prototypes
global function str_room f_new_waiting_room (string ps_office_id)
end prototypes

global function str_room f_new_waiting_room (string ps_office_id);str_popup popup
str_popup_return popup_return
long ll_sts
long ll_count
u_ds_data luo_data
str_room lstr_room
string ls_room_id
string ls_room_name
string ls_min_room_id
string ls_status
string ls_room_type

lstr_room = f_empty_room()

SELECT count(*)
INTO :ll_count
FROM o_Rooms
WHERE room_name = 'Sick'
AND office_id = :ps_office_id;
if not tf_check() then return lstr_room
if ll_count = 0 then
	popup.data_row_count += 1
	popup.items[popup.data_row_count] = 'Sick'
end if

SELECT count(*)
INTO :ll_count
FROM o_Rooms
WHERE room_name = 'Well'
AND office_id = :ps_office_id;
if not tf_check() then return lstr_room
if ll_count = 0 then
	popup.data_row_count += 1
	popup.items[popup.data_row_count] = 'Well'
end if

if popup.data_row_count > 0 then
	popup.data_row_count += 1
	popup.items[popup.data_row_count] = "<Other>"
	openwithparm(w_pop_pick, popup)
	popup_return = message.powerobjectparm
	if popup_return.item_count <> 1 then return lstr_room
	
	ls_room_name = popup_return.items[1]
	if ls_room_name = "<Other>" then ls_room_name = ""
else
	ls_room_name = ""
end if

DO WHILE true
	if ls_room_name = "" then
		popup.dataobject = ""
		popup.datacolumn = 0
		popup.displaycolumn = 24
		popup.title = "Enter the name of the new room"
		openwithparm(w_pop_prompt_string, popup)
		popup_return = message.powerobjectparm
		if popup_return.item_count <> 1 then return lstr_room
		
		ls_room_name = popup_return.items[1]
	end if
	
	SELECT count(*), min(room_id)
	INTO :ll_count, :ls_min_room_id
	FROM o_Rooms
	WHERE room_name = :ls_room_name
	AND office_id = :ps_office_id;
	if not tf_check() then return lstr_room
	
	if ll_count =  0 then
		lstr_room.room_name = ls_room_name
		exit
	end if
	
	SELECT status, room_type
	INTO :ls_status, :ls_room_type
	FROM o_Rooms
	WHERE room_id = :ls_min_room_id;
	if not tf_check() then return lstr_room

	if upper(ls_status) = "NA" and ls_room_type = "$WAITING" then
		openwithparm(w_pop_yes_no, "That room name already exists but is disabled.  Do you wish to enable this room for this office?")
		popup_return = message.powerobjectparm
		if popup_return.item <> "YES" then
			ls_room_name = ""
			continue
		end if
		
		UPDATE o_Rooms
		SET office_id = :ps_office_id,
				status = 'OK'
		WHERE room_id = :ls_min_room_id;
		if not tf_check() then return lstr_room
		lstr_room.room_type = "$WAITING"
		lstr_room.room_name = ls_room_name
		lstr_room.room_id = ls_min_room_id
		return lstr_room
	else
		openwithparm(w_pop_message, "That room name is already in use.  Please enter a unique room name.")
		ls_room_name = ""
		continue
	end if
LOOP


lstr_room.room_type = "$WAITING"

sqlca.jmj_new_room(lstr_room.room_type, lstr_room.room_name, ps_office_id, ref ls_room_id)
if not tf_check() then return lstr_room

lstr_room.room_id = ls_room_id

return lstr_room

end function

