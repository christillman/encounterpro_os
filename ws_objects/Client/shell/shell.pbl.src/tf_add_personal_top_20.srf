$PBExportHeader$tf_add_personal_top_20.srf
global type tf_add_personal_top_20 from function_object
end type

forward prototypes
global function integer tf_add_personal_top_20 (string ps_top_20_code, string ps_item_text, string ps_item_id, string ps_item_id2, long pl_item_id3)
end prototypes

global function integer tf_add_personal_top_20 (string ps_top_20_code, string ps_item_text, string ps_item_id, string ps_item_id2, long pl_item_id3);long ll_sort_sequence
string ls_user_id
long ll_count

if isnull(current_user) then
	log.log(this, "tf_add_personal_top_20.0006", "No Current User", 3)
	return 0
end if

ls_user_id = current_user.user_id

SELECT max(sort_sequence)
INTO :ll_sort_sequence
FROM u_Top_20
WHERE user_id = :ls_user_id
AND top_20_code = :ps_top_20_code;
if not tf_check() then return -1

if isnull(ll_sort_sequence) then
	ll_sort_sequence = 1
else
	ll_sort_sequence += 1
end if

SELECT count(*)
INTO :ll_count
FROM u_Top_20
WHERE user_id = :ls_user_id
AND top_20_code = :ps_top_20_code
AND ISNULL(item_text, '!NULL') = ISNULL(:ps_item_text, '!NULL')
AND ISNULL(item_id, '!NULL') = ISNULL(:ps_item_id, '!NULL')
AND ISNULL(item_id2, '!NULL') = ISNULL(:ps_item_id2, '!NULL')
AND ISNULL(item_id3, -9999) = ISNULL(:pl_item_id3, -9999);
if not tf_check() then return -1

// If the record is already there then return success
if ll_count > 0 then return 1

INSERT INTO u_Top_20 (
	user_id,
	top_20_code,
	sort_sequence,
	item_text,
	item_id,
	item_id2,
	item_id3)
VALUES (
	:ls_user_id,
	:ps_top_20_code,
	:ll_sort_sequence,
	:ps_item_text,
	:ps_item_id,
	:ps_item_id2,
	:pl_item_id3);
if not tf_check() then return -1


return 1

end function

