$PBExportHeader$f_edit_mapping.srf
global type f_edit_mapping from function_object
end type

forward prototypes
global function long f_edit_mapping (long pl_code_id, string ps_which)
end prototypes

global function long f_edit_mapping (long pl_code_id, string ps_which);//
// ps_which = "code" or "epro_id"      - identifies which side of the mapping is getting modified
//
//

string ls_current_epro_id
string ls_new_epro_id

string ls_current_code
string ls_new_code

string ls_current_code_description
string ls_new_code_description

string ls_current_epro_description
string ls_new_epro_description

long ll_owner_id
string ls_code_domain
string ls_code_version
string ls_epro_domain
long ll_new_code_id
str_domain_item lstr_domain_item

if isnull(pl_code_id) then
	log.log(this, "f_edit_mapping()", "null code_id", 4)
	return -1
end if

// Get the details of the mapping
SELECT c.owner_id, c.code_domain, c.code_version, c.code, c.code_description, c.epro_domain, c.epro_id, c.epro_description
INTO :ll_owner_id, :ls_code_domain, :ls_code_version, :ls_current_code, :ls_current_code_description, :ls_epro_domain, :ls_current_epro_id, :ls_current_epro_description
FROM c_XML_Code c
WHERE c.code_id = :pl_code_id;
if not tf_check() then return -1
if sqlca.sqlnrows < 1 then
	log.log(this, "f_edit_mapping()", "code_id not found (" + string(pl_code_id) + ")", 4)
	return -1
end if

if lower(ps_which) = "code" then
	lstr_domain_item = f_pick_interface_domain_item(ll_owner_id, ls_code_domain, "Select the new " + ls_code_domain + " value to map to ~"" + ls_current_epro_id + "~".")

	if isnull(lstr_domain_item.domain_item) then return 0
	if lstr_domain_item.domain_item = ls_current_code then return 0

	ls_new_code = lstr_domain_item.domain_item
	ls_new_code_description = lstr_domain_item.domain_item_description
	
	ls_new_epro_id = ls_current_epro_id
	ls_new_epro_description = ls_current_epro_description
else
	lstr_domain_item = f_pick_domain_item(ls_epro_domain, "Select the new " + ls_epro_domain + " value to map to ~"" + ls_current_code + "~".")

	if isnull(lstr_domain_item.domain_item) then return 0
	if lstr_domain_item.domain_item = ls_current_epro_id then return 0

	ls_new_epro_id = lstr_domain_item.domain_item
	ls_new_epro_description = lstr_domain_item.domain_item_description
	
	ls_new_code = ls_current_code
	ls_new_code_description = ls_current_code_description
end if

ll_new_code_id = sqlca.xml_add_mapping(ll_owner_id, ls_code_domain, ls_code_version, ls_new_code, ls_new_code_description, ls_epro_domain, ls_new_epro_id, ls_new_epro_description, lstr_domain_item.domain_item_owner_id, current_scribe.user_id)
if not tf_check() then return -1

return ll_new_code_id

end function

