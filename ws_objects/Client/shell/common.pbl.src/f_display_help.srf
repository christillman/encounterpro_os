$PBExportHeader$f_display_help.srf
global type f_display_help from function_object
end type

forward prototypes
global subroutine f_display_help (string ps_help_context, string ps_which_help, boolean pb_edit)
end prototypes

global subroutine f_display_help (string ps_help_context, string ps_which_help, boolean pb_edit);long ll_choice
//long ll_count
//string lstr_article.article
string ls_host
//string ls_epro_domain
string ls_helpdisplaywebsite
string ls_helpcreatewebsite
string ls_helpURL
str_popup popup
str_popup_return popup_return
string ls_question
str_c_xml_code lstr_xml_code
//boolean lb_user_may_edit
boolean lb_new_map
string ls_help_home_url
str_help_article lstr_article

lstr_article = f_display_help_article(ps_help_context, ps_which_help)

ls_help_home_url = datalist.get_preference("SYSTEM", "Help Home URL", "http://solutioncenter.encounterpro.net:8080/selfservice/portal?page=default.psml")

lb_new_map = false
if pb_edit or isnull(lstr_article.article) then
	// If the user is not authorized to map/edit the exit
	if not lstr_article.user_may_edit then
		if pb_edit then
			return
		else
			openwithparm(w_pop_yes_no, "This context does not have a help article defined.  Would you like to see the Solutiion Center Home Page?")
			popup_return = message.powerobjectparm
			if popup_return.item <> "YES" then return
			
			// Solution Center Home Page
			f_open_browser(ls_help_home_url)
			return
		end if
	end if
	
	// If the user is trying to edit, then offer the choice of changing the mapping or editing the existing article
	if pb_edit and len(lstr_article.article) > 0 then
		popup.title = "This context is mapped to the help article ~"" + lstr_article.article + "~".  Do you wish to..."
		popup.data_row_count = 3
		popup.items[1] = "Edit this article"
		popup.items[2] = "Map this context to a different article"
		popup.items[3] = "Cancel"
		openwithparm(w_pop_choices_3, popup)
		ll_choice = message.doubleparm
		CHOOSE CASE ll_choice
			CASE 1
				pb_edit = true
				lb_new_map = false
			CASE 2
				pb_edit = false
				lb_new_map = true
			CASE ELSE
				return
		END CHOOSE
	else
		// See if the user wants to assign an article name to this help context
		ls_question = "This context does not have an article name assigned to it.  Do you wish to assign an article to this context?"
		openwithparm(w_pop_yes_no, ls_question)
		popup_return = message.powerobjectparm
		if popup_return.item <> "YES" then
			openwithparm(w_pop_yes_no, "Would you like to see the Solutiion Center Home Page?")
			popup_return = message.powerobjectparm
			if popup_return.item <> "YES" then return
			
			// Solution Center Home Page
			f_open_browser(ls_help_home_url)
			return
		end if
		
		lb_new_map = true
	end if

	if lb_new_map then
		popup.title = "Enter the name of the " + lower(ps_which_help) + " article for this context"
		popup.data_row_count = 0
		openwithparm(w_pop_prompt_string, popup)
		popup_return = message.powerobjectparm
		if popup_return.item_count <> 1 then return
		
		lstr_article.article = popup_return.items[1]
		
		lstr_xml_code = f_empty_xml_code()
		
		lstr_xml_code.owner_id = 0
		lstr_xml_code.code_domain =  "EproHelpContext"
		setnull(lstr_xml_code.code_version)
		lstr_xml_code.code = ps_help_context
		lstr_xml_code.code_description = "Epro Context: " + ps_help_context
		lstr_xml_code.epro_owner_id = 0
		lstr_xml_code.epro_domain = lstr_article.epro_domain
		lstr_xml_code.epro_id = lstr_article.article
		lstr_xml_code.epro_description = "Help Article: " + lstr_article.article
		lstr_xml_code.unique_flag = 1
		lstr_xml_code.created_by = windows_logon_id
		lstr_xml_code.mapping_owner_id = 0
		lstr_xml_code.status = "OK"
		lstr_xml_code.description = "Help Article Mapping"
		
		datalist.xml_add_mapping(lstr_xml_code)
		
		// Copy the code to the EproUpdates c_XML_Code table
		INSERT INTO jmjtech.eproupdates.dbo.c_XML_Code (
				  owner_id
				  ,code_domain
				  ,code_version
				  ,code
				  ,code_description
				  ,epro_owner_id
				  ,epro_domain
				  ,epro_id
				  ,epro_description
				  ,unique_flag
				  ,created_by
				  ,mapping_owner_id
				  ,status
				  ,description)
		  VALUES (
				:lstr_xml_code.owner_id,
				:lstr_xml_code.code_domain,
				:lstr_xml_code.code_version,
				:lstr_xml_code.code, 
				:lstr_xml_code.code_description, 
				:lstr_xml_code.epro_owner_id,
				:lstr_xml_code.epro_domain, 
				:lstr_xml_code.epro_id,
				:lstr_xml_code.epro_description, 
				:lstr_xml_code.unique_flag,
				:lstr_xml_code.created_by, 
				:lstr_xml_code.mapping_owner_id,
				:lstr_xml_code.status,
				:lstr_xml_code.description);
			if not tf_check() then return
	end if
end if



// e.g.  http://solutioncenter.encounterpro.net:8080/selfservice/rassp/KPListing/view2.jsp?k2dockey=040818616101558
ls_helpdisplaywebsite = datalist.get_preference("SYSTEM", "Help Display URL", "http://%host%/selfservice/rassp/KPListing/view2.jsp?k2dockey=%article%")

// e.e. https://content.rightanswers.com/onlinekm45/solutionview.jsp?solutionid=%article%&solutionselected=true
ls_helpcreatewebsite = datalist.get_preference("SYSTEM", "Help Edit URL", "https://content.rightanswers.com/onlinekm45/solutionview.jsp?solutionid=%article%&solutionselected=true")

// While we test the system
//ls_host = datalist.get_preference("SYSTEM", "Help Host", "wss-vm:101")
// After the provider portal is published
ls_host = datalist.get_preference("SYSTEM", "Help Host", "solutioncenter.encounterpro.net:8080")


// Substitute in the article name
if pb_edit then
	ls_helpURL = ls_helpcreatewebsite
else
	ls_helpURL = ls_helpdisplaywebsite
end if

// Sub in the host name
ls_helpURL = f_string_substitute(ls_helpURL, "%host%", ls_host)

// Before subbing in the article, substitute any spaces for "%20"
lstr_article.article = f_string_substitute(lstr_article.article, " ", "%20")
ls_helpURL = f_string_substitute(ls_helpURL, "%article%", lstr_article.article)

f_open_browser(ls_helpURL)


end subroutine

