$PBExportHeader$f_pick_interface_domain_item.srf
global type f_pick_interface_domain_item from function_object
end type

forward prototypes
global function str_domain_item f_pick_interface_domain_item (long pl_owner_id, string ps_code_domain, string ps_helptext)
end prototypes

global function str_domain_item f_pick_interface_domain_item (long pl_owner_id, string ps_code_domain, string ps_helptext);str_popup popup
str_popup_return popup_return
long i
str_param_setting lstr_param
str_param_wizard_return lstr_return
long ll_count
string ls_domain_description
boolean lb_is_param
string ls_param_class
string ls_param_query
string ls_null
str_domain_item lstr_domain_item
long ll_code_domain_item_owner_id
string ls_description
string ls_epro_domain
string ls_status
long ll_code_domain_owner_id
string ls_map_cardinality

setnull(ls_null)
lstr_domain_item = f_empty_domain_item()

// Get the details of the mapping
SELECT description, epro_domain, status, code_domain_owner_id, map_cardinality
INTO :ls_description, :ls_epro_domain, :ls_status, :ll_code_domain_owner_id, :ls_map_cardinality
FROM c_XML_Code_Domain
WHERE owner_id = :pl_owner_id
AND code_domain = :ps_code_domain;
if not tf_check() then return lstr_domain_item
if sqlca.sqlnrows < 1 then
	// No domain records means manual input
	popup.argument_count = 0
	popup.title = ps_helptext
	popup.item = ""
	popup.displaycolumn = 64
	openwithparm(w_pop_prompt_string, popup)
	popup_return = message.powerobjectparm
	if popup_return.item_count <> 1 then return lstr_domain_item
	
	lstr_domain_item.domain_item = popup_return.items[1]
	return lstr_domain_item
end if

popup.dataobject = "dw_c_xml_code_domain_item"
popup.argument_count = 2
popup.numeric_argument = true
popup.argument[1] = string(pl_owner_id)
popup.argument[2] = ps_code_domain
popup.add_blank_row = true
popup.blank_text = "<Other Value>"
popup.blank_at_bottom = true
popup.auto_singleton = true
popup.datacolumn = 5
popup.displaycolumn = 6
openwithparm(w_pop_pick, popup)
popup_return = message.powerobjectparm
if popup_return.item_count <> 1 then return lstr_domain_item
if popup_return.items[1] = "" then
	// Other Value
	popup.argument_count = 0
	popup.title = ps_helptext
	popup.item = ""
	popup.displaycolumn = 64
	openwithparm(w_pop_prompt_string, popup)
	popup_return = message.powerobjectparm
	if popup_return.item_count <> 1 then return lstr_domain_item
	
	lstr_domain_item.domain_item = popup_return.items[1]
else
	lstr_domain_item.domain_item = popup_return.items[1]
	lstr_domain_item.domain_item_description = popup_return.descriptions[1]
end if

// If we don't have a new epro_id then return 0
if trim(lstr_domain_item.domain_item) = "" then
	setnull(lstr_domain_item.domain_item)
end if

if isnull(lstr_domain_item.domain_item_description) then
	lstr_domain_item.domain_item_description = lstr_domain_item.domain_item
end if

SELECT min(code_domain_item_owner_id)
INTO :ll_code_domain_item_owner_id
FROM c_XML_Code_Domain_Item
WHERE owner_id = :pl_owner_id
AND code_domain = :ps_code_domain
AND code = :lstr_domain_item.domain_item
AND status = 'OK';
if not tf_check() then return lstr_domain_item

lstr_domain_item.domain_owner_id = ll_code_domain_owner_id
lstr_domain_item.domain_item_owner_id = ll_code_domain_item_owner_id
lstr_domain_item.domain = ps_code_domain

return lstr_domain_item


end function

