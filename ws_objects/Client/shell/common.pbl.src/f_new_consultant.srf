$PBExportHeader$f_new_consultant.srf
global type f_new_consultant from function_object
end type

forward prototypes
global function string f_new_consultant ()
end prototypes

global function string f_new_consultant ();str_popup popup
str_popup popup2
str_popup popup3
str_popup_return popup_return
string ls_consultant_id
long ll_row
integer li_sts
string ls_description
long ll_nextkey
string ls_null
string ls_user_id
long ll_count
long ll_iterations
string ls_specialty_id

setnull(ls_null)

popup.title = "Select specialty of new consultant"
popup.dataobject = "dw_specialty_list"
popup.datacolumn = 2
popup.displaycolumn = 1
openwithparm(w_pop_pick, popup)
popup_return = message.powerobjectparm
if popup_return.item_count <> 1 then return ls_null

ls_specialty_id = popup_return.items[1]

popup2.title = "Enter consultant name or description"
openwithparm(w_pop_prompt_string, popup2)
popup_return = message.powerobjectparm
if popup_return.item_count <> 1 then return ls_null

ls_description = trim(popup_return.items[1])
if ls_description = "" then return ls_null

ll_iterations = 0
DO WHILE true
	ll_iterations += 1
	if ll_iterations = 1000 then return ls_null
	
	sqlca.sp_get_next_key("!CPR", "USER_ID", ll_nextkey)
	if not tf_check() then return ls_null

	ls_user_id = string(sqlca.customer_id) + "^" + string(ll_nextkey)
	
	SELECT count(*)
	INTO :ll_count
	FROM c_User
	WHERE user_id = :ls_user_id;
	if not tf_check() then return ls_null
	
	IF ll_count = 0 then exit
LOOP

INSERT INTO c_Consultant (
	consultant_id,
	specialty_id,
	description)
VALUES (
	:ls_user_id,
	:ls_specialty_id,
	:ls_description);
if not tf_check() then return ls_null

//Openwithparm(w_edit_consultant, ls_user_id)
popup3.data_row_count = 1
popup3.items[1] = ls_user_id
openwithparm(w_user_definition, popup3)

return ls_user_id

end function

