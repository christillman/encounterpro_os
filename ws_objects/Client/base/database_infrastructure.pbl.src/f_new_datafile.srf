$PBExportHeader$f_new_datafile.srf
global type f_new_datafile from function_object
end type

forward prototypes
global function string f_new_datafile (str_attributes pstr_attributes)
end prototypes

global function string f_new_datafile (str_attributes pstr_attributes);str_popup popup
str_popup_return popup_return
string ls_context_object
string ls_description
string ls_datafile_document_component_id
string ls_report_id
string ls_null
long ll_sts
long ll_count
string ls_long_description
string ls_datafile_report_component_id
string ls_datafile_report_component_guid

ls_datafile_report_component_id = "RPT_Document"
setnull(ls_null)
setnull(ls_long_description)

DO WHILE true
	popup.title = "Enter title of new datafile"
	openwithparm(w_pop_prompt_string, popup)
	popup_return = message.powerobjectparm
	if popup_return.item_count <> 1 then return ls_null
	
	ls_description = popup_return.items[1]
	
	SELECT count(*)
	INTO :ll_count
	FROM c_Report_Definition
	WHERE description = :ls_description;
	if not tf_check() then return ls_null
	if ll_count > 0 then
		openwithparm(w_pop_message, "There is already a datafile or report with that title.  Please enter a different title for the new datafile.")
	else
		exit
	end if
LOOP

popup.title = "Select Datafile Context"
popup.dataobject = "dw_domain_notranslate_list"
popup.datacolumn = 2
popup.displaycolumn = 2
popup.argument_count = 1
popup.argument[1] = "CONTEXT_OBJECT"
openwithparm(w_pop_pick, popup)
popup_return = message.powerobjectparm
if popup_return.item_count <> 1 then return ls_null

ls_context_object = popup_return.items[1]

popup.title = "Select Datafile Component"
popup.dataobject = "dw_component_pick"
popup.datacolumn = 1
popup.displaycolumn = 4
popup.argument_count = 1
popup.argument[1] = "Document"
openwithparm(w_pop_pick, popup)
popup_return = message.powerobjectparm
if popup_return.item_count <> 1 then return ls_null

ls_datafile_document_component_id = popup_return.items[1]

SELECT CAST(id AS varchar(40))
INTO :ls_datafile_report_component_guid
FROM dbo.fn_components()
WHERE component_id = :ls_datafile_report_component_id;
if not tf_check() then return ls_null

ll_sts = sqlca.jmj_new_document_config_object(  &
										"Datafile", &
										ls_description, &
										ls_context_object, &
										ls_datafile_document_component_id, &
										current_user.user_id, &
										"OK", &
										ls_long_description, &
										REF ls_report_id )
if not tf_check() then return ls_null
if ll_sts <> 1 then
	log.log(po_null, "f_new_datafile:0079", "Error creating new datafile", 4)
	return ls_null
end if

if isnull(ls_report_id) or trim(ls_report_id) = "" then
	log.log(po_null, "f_new_datafile:0084", "jmj_new_datafile returned NULL", 4)
	return ls_null
end if

return ls_report_id

end function

